plugins {
    id 'java'
    id 'jacoco'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version "${shadowVersion}"
}

group 'org.obapanel.jedis'
version "${projectVersion}"

sourceCompatibility = "${javaVersion}"
targetCompatibility = "${javaVersion}"

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation "redis.clients:jedis:${jedisVersion}"
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation "org.slf4j:slf4j-simple:${slf4jVersion}"
    implementation group: 'org.javassist', name: 'javassist', version: "${javassistVersion}"

    testImplementation group: 'junit', name: 'junit', version: "${junitVersion}"
    testImplementation group: 'org.mockito', name: 'mockito-core', version: "${mockitoVersion}"
    testImplementation group: 'org.powermock', name: 'powermock-core', version: "${powermockVersion}"
    testImplementation group: 'org.powermock', name: 'powermock-api-mockito2', version: "${powermockVersion}"
    testImplementation group: 'org.powermock', name: 'powermock-module-junit4', version: "${powermockVersion}"

    // sadly doesnt work => testImplementation 'com.github.fppt:jedis-mock:0.1.14'
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/oscar-besga-panel/InterruptingJedisLocks")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

shadowJar {
    manifest {
        attributes 'Description': 'Interrupting Locks all'
    }
}